plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'

    // keep dependencies up-to-date!
    id 'com.github.ben-manes.versions' version '0.41.0'

    // useful for creating immutable java beans
    id 'org.inferred.processors' version '3.6.0'

    // to ensure clean code
    id "net.ltgt.errorprone" version "2.0.2"

    // for building fat jar
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group   = 'com.umutsahin'
version = '1.3.1'

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/umutsahin/java-jq"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

dependencies {
    processor 'org.immutables:value:2.9.0'

    // annotations
    api 'org.immutables:value-annotations:2.9.0'
    
    // configure errorprone version
    errorprone 'com.google.errorprone:error_prone_core:2.11.0'

    // we shadow Guava due to frequent downstream compatibility issues
    shadow      'com.google.guava:guava:31.0.1-jre'

    // we do not shadow this library because there may be linkages to native code that
    // rely upon specific package or class names
    shadow      'net.java.dev.jna:jna:5.10.0'

    // logging
    testImplementation 'org.apache.logging.log4j:log4j-api:2.17.1'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.17.1'
    testImplementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.17.1'
    testImplementation 'org.slf4j:jcl-over-slf4j:1.7.32'
    testImplementation 'org.slf4j:jul-to-slf4j:1.7.32'
    api                'org.slf4j:slf4j-api:1.7.32'

    // for unit testing
    testImplementation 'com.fasterxml.jackson.core:jackson-core:2.13.1'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
    testImplementation 'com.fasterxml.jackson.module:jackson-module-parameter-names:2.13.1'
    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.13.1'
    testImplementation 'net.javacrumbs.json-unit:json-unit:2.28.0'
    testImplementation 'com.google.guava:guava:31.0.1-jre'

    // for unit testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
}

shadowJar {
    // need to update any META-INF/services to relocated class names
    mergeServiceFiles()

    // we don't want poms for third-party stuff
    exclude 'META-INF/maven/**/*'
    exclude 'META-INF/**/*.SF'
    exclude 'META-INF/**/*.DSA'
    exclude 'META-INF/**/*.RSA'

    // we are only shadowing 'shadow' dependencies
    configurations = [
        project.configurations.shadow
    ]

    relocate 'com.google.common',       'repackaged.com.arakelian.jq.com.google.common'
    relocate 'com.google.thirdparty',   'repackaged.com.arakelian.jq.com.google.thirdparty'
    relocate 'javax.annotation',        'repackaged.com.arakelian.jq.javax.annotation'
    relocate 'org.apache.commons',      'repackaged.com.arakelian.jq.org.apache.commons'
    relocate 'org.checkerframework',    'repackaged.com.arakelian.jq.org.checkerframework'
}
